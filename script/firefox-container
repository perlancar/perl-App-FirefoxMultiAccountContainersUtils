#!perl

#use 5.010001;
use strict;
use warnings;
use Log::ger::Screen;
use Log::ger;

# AUTHORITY
# DATE
# DIST
# VERSION

my $container;
if (defined(my $n = $ENV{FIREFOX_CONTAINER_ENV})) {
    if (defined(my $c = $ENV{$n})) {
        log_trace "Using container '$c' (from $n env (from FIREFOX_CONTAINER_ENV env))";
        $container = $c;
    } else {
        log_trace "No container set in $n env (set via FIREFOX_CONTAINER_ENV env)";
    }
} elsif (defined(my $c = $ENV{FIREFOX_CONTAINER})) {
    log_trace "Using container '$c' (from FIREFOX_CONTAINER env))";
    $container = $c;
} else {
    log_trace "No container set";
}

my @exec_cmd;
if (defined $container) {
    @exec_cmd = ("open-firefox-container", $container, @ARGV);
} else {
    @exec_cmd = ("firefox", @ARGV);
}
log_trace "Exec: %s", \@exec_cmd;
exec @exec_cmd;

# ABSTRACT: Firefox wrapper to open URL's in a specific container
# PODNAME:

=head1 SYNOPSIS

Use like you would use L<firefox> command:

 % FIREFOX_CONTAINER=mycontainer firefox-container [options] <URL>...

Or set it as your BROWSER:

 % export BROWSER=firefox-container

then use it with commands that open URL's in browser, e.g.
L<this-repo-on-github>:

 % FIREFOX_CONTAINER=mycontainer this-repo-on-github


=head1 DESCRIPTION

This is a simple wrapper for B<firefox>. If the environment variable
L</FIREFOX_CONTAINER> is defined, will open using L<open-firefox-container>.
Otherwise, will simply exec C<firefox>.


=head1 OPTIONS

No other options are parsed by this wrapper. All will be passed to C<firefox>.


=head1 ENVIRONMENT

=head2 FIREFOX_CONTAINER

Set the name of container to open URL's in.

=head2 FIREFOX_CONTAINER_ENV

Set the name of environment variable to replace the default
L</FIREFOX_CONTAINER>. Why would one want to do this? Because one will be able
to use a shorter name for L</FIREFOX_CONTAINER>. So instead of this:

 % FIREFOX_CONTAINER=mycontainer1 firefox-container ...
 % FIREFOX_CONTAINER=mycontainer2 other-command ...

you can instead use this:

 % export FIREFOX_CONTAINER_ENV=FC
 % FC=mycontainer1 firefox-container ...
 % FC=mycontainer2 other-command ...


=head1 SEE ALSO

L<open-firefox-container>
